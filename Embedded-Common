# message("*** ${CMAKE_CURRENT_LIST_FILE} ***")

# This is a common file to be shared among different language 'Platform/${CMAKE_SYSTEM_NAME}-<LANG>' boot files
cmake_minimum_required(VERSION 3.10)

macro(check_embedded_variables lang)
    if(NOT DEFINED EMBEDDED_${lang}_COMPILER)
        message(FATAL_ERROR "EMBEDDED_${lang}_COMPILER is not defined in the project's CMakeLists.txt")
    endif()
    if(NOT DEFINED EMBEDDED_${lang}_COMPILER_VERSION)
        set(EMBEDDED_${lang}_COMPILER_VERSION *)
    endif()
endmacro()

macro(find_embedded_compiler lang id names hint_paths)
    set(CMAKE_${lang}_COMPILER_ID ${id})
    set(CMAKE_${lang}_COMPILER_NAMES ${names})
    unset(CMAKE_${lang}_COMPILER)
    foreach(hint_path ${hint_paths})
        file(GLOB paths LIST_DIRECTORIES true ${hint_path})
        foreach(path ${paths})
            if(IS_DIRECTORY ${path})
                find_program(compiler ${names} PATHS ${path})
                if (NOT "${compiler}" STREQUAL "compiler-NOTFOUND")
                    set(CMAKE_${lang}_COMPILER ${compiler})
                endif ()
            endif()
        endforeach()
    endforeach()
endmacro()

macro(check_embedded_compiler_is_set lang)
    if(NOT DEFINED CMAKE_${lang}_COMPILER_ID)
        message(FATAL_ERROR "CMAKE_${lang}_COMPILER_ID not set")
    endif()
    if(NOT DEFINED CMAKE_${lang}_COMPILER_NAMES)
        message(FATAL_ERROR "CMAKE_${lang}_COMPILER_NAMES not set")
    endif()
    if(NOT DEFINED CMAKE_${lang}_COMPILER)
        message(FATAL_ERROR "Compiler binary ${CMAKE_${lang}_COMPILER_NAMES} not found!")
    endif()
    message(STATUS "${CMAKE_${lang}_COMPILER_ID} compiler found at ${CMAKE_${lang}_COMPILER}")
endmacro()
